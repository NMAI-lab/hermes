apply plugin: 'java'
apply plugin: 'org.ros2.tools.gradle'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'org.ros2.tools.gradle:ament:0.8.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['config/']
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
    implementation 'io.github.jason-lang:jason-interpreter:3.2.0'
    implementation 'org.json:json:20231013'
}

shadowJar {
    classifier = ''
}

ament {
    entryPoints {
        consoleScripts = [
            'hermes_agent = org.ros2.hermes.agent.HermesAgent'
        ]
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    destinationDir = new java.io.File(ament.installSpace + "/share/" + ament.packageManifestName + "/java")
    from sourceSets.main.allSource
}

tasks.getByName('sourcesJar') {
    dependsOn shadowJar

    doLast {
        copy {
            from(shadowJar.archivePath)
            into "${ament.installSpace}/share/${ament.packageManifestName}/java"
            rename { 'hermes_agent.jar' }
        }
    }
}

artifacts {
    archives sourcesJar
}